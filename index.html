<html>
<meta charset="UTF-8">
<title>Reaction Diffustion System</title>
<head>
	<link rel="stylesheet" type="text/css" href="css/reaction-diffusion.css"/>
	<script type="text/javascript" src="js/dat.gui.min.js"></script>
	<script type="text/javascript" src="js/reaction-diffusion.js"></script>
	<script id="rd-vertex-shader" type="x-shader/x-vertex">
		precision mediump float;

		attribute vec2 a_position;
		attribute vec2 a_texCoord;

		uniform vec2 u_resolution;

		varying vec2 v_texCoord;

		void main() {
			// convert the rectangle from pixels to 0.0 to 1.0
			vec2 zeroToOne = a_position / u_resolution;

			// convert from 0->1 to -1 -> 1
			vec2 clipSpace = (zeroToOne * 2.0) - 1.0;

			gl_Position = vec4(clipSpace, 0, 1);

			// pass the texCoord to the fragment shader. The GPU will interpolate this value between points.
			v_texCoord = a_texCoord;
		}
	</script>
	<script id="rd-fragment-shader" type="x-shader/x-fragment">
		precision mediump float;

		uniform vec2 u_resolution;

		// mouse location
		uniform vec2 u_mouse;

		// feed rate
		uniform float u_feed;
		// kill rate
		uniform float u_kill;

		// texture
		uniform sampler2D u_texture;

		// the texCoords passed in from the vertex shader.
		varying vec2 v_texCoord;

		float diffusion_rate_A = 1.0;
		float diffusion_rate_B = 0.5;

		float dt = 1.0;

		void main() {

			if(u_mouse.x > -2.0 && abs(u_mouse.x - v_texCoord.x) < 0.0015 && abs(u_mouse.y - (1.0 - v_texCoord.y)) < 0.0015) {
				gl_FragColor = vec4(0.0, 1.0, 0.0, 1.0);
			} else {
				float dx = 1.0/u_resolution.x;
				float dy = 1.0/u_resolution.y;

				vec2 c0 = texture2D(u_texture, v_texCoord + vec2(-dx,-dy)).rg;
				vec2 c1 = texture2D(u_texture, v_texCoord + vec2(0,-dy)).rg;
				vec2 c2 = texture2D(u_texture, v_texCoord + vec2(dx,-dy)).rg;
				vec2 c3 = texture2D(u_texture, v_texCoord + vec2(-dx,0)).rg;
				vec2 c4 = texture2D(u_texture, v_texCoord).rg;
				vec2 c5 = texture2D(u_texture, v_texCoord + vec2(dx,0)).rg;
				vec2 c6 = texture2D(u_texture, v_texCoord + vec2(-dx,dy)).rg;
				vec2 c7 = texture2D(u_texture, v_texCoord + vec2(0,dy)).rg;
				vec2 c8 = texture2D(u_texture, v_texCoord + vec2(dx,dy)).rg;

				// center weight -1, adjacent neighbors .2, and diagonals .05
				vec2 laplacian = (0.05*(c0+c2+c6+c8)) + (0.2*(c1+c3+c5+c7) - c4);

				// diffision: a and b with both diffuse at different rates
				// reaction: one a and bs will form one b, so b is added based on how much a and b there is in the simulation.
				float a = c4.r + (diffusion_rate_A*laplacian.r - c4.r*c4.g*c4.g + u_feed*(1.0-c4.r))*dt;
				float b = c4.g + (diffusion_rate_B*laplacian.g + c4.r*c4.g*c4.g - (u_feed+u_kill)*c4.g)*dt;

				// look up the color
				gl_FragColor = vec4(a, b, 0.0, 1.0);
				//gl_FragColor = texture2D(u_texture, v_texCoord);
			}
		}
	</script>
	<script id="screen-fragment-shader" type="x-shader/x-fragment">
		precision mediump float;

		// texture
		uniform sampler2D u_texture;

		// the texCoords passed in from the vertex shader.
		varying vec2 v_texCoord;

		void main() {
			vec2 ab = texture2D(u_texture, v_texCoord).rg;

			// TODO: flip red and green.  is this working?
			gl_FragColor = vec4(ab.g, ab.r, 0.0, 1.0);
		}
	</script>
</head>
<body>
	<canvas id="canvas" tabindex="1"></canvas>
	<canvas id="tex0" style="display: none"></canvas>
	<canvas id="tex1" style="display: none"></canvas>
</body>
</html>
